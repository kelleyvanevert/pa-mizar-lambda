::: # Lambda calculus

:: Environ originally taken from `finseq_2`. Later, I added trees and orders.
environ

  :: Vocabularies are basically for Mizar's lexer.
  vocabularies LAMBDA,
    FINSET_1,
    NUMBERS,
    NAT_1, XBOOLE_0, SUBSET_1, FINSEQ_1, XXREAL_0, CARD_1,
    ARYTM_1, ARYTM_3, TARSKI, ORDINAL4, FUNCOP_1,
    RELAT_2, RELAT_1,
    FUNCT_1, FUNCT_2,
    ZFMISC_1, PARTFUN1,
    ORDERS_2, ORDERS_1,
    FINSEQ_2, PBOOLE, CARD_3, VALUED_1,
    TREES_4, TREES_3, TREES_2, TREES_1;
  
  :: Radix type specifications
  :: But it also helped [175 unknown attribute format] error with [finite set]
  notations FINSET_1,
    TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, ORDINAL1, CARD_1,
    NUMBERS, XCMPLX_0, DOMAIN_1, NAT_1, RELAT_1, PARTFUN1,
    RELAT_2, RELSET_1,
    FUNCT_1, FUNCT_2,
    FUNCT_3, FINSEQ_1, FINSEQ_2, BINOP_1, PBOOLE, FUNCOP_1, CARD_3,
    TREES_4, TREES_3, TREES_2, TREES_1,
    ORDERS_1, ORDERS_2,
    XXREAL_0, VALUED_1,
    RECDEF_1;

  :: For modes? (unknown mode format resolved)
  constructors RELAT_2, RELAT_1,
    PARTFUN1, BINOP_1, DOMAIN_1,
    FUNCT_1, FUNCT_2, FUNCT_3,
    FUNCOP_1,
    RELSET_1,
    SQUARE_1, NAT_1, FINSEQ_1, FINSEQ_2, PBOOLE, CARD_3, VALUED_1, SETFAM_1,
    TREES_4, TREES_3, TREES_2, TREES_1,
    ORDERS_1, ORDERS_2,
    RECDEF_1,
    FINSET_1,
    ENUMSET1,
    WELLORD2;

  :: Definition are for automatically unfolding predicates
  ::  in the thesis you are proving
  definitions TARSKI,
    FUNCT_1, FUNCT_2,
    FINSEQ_1, CARD_1, PBOOLE, FUNCOP_1,
    ORDERS_1,
    TREES_4, TREES_3, TREES_2, TREES_1,
    XTUPLE_0;

  :: Registrations are for clusters (showing that a type exists (is non-empty),
  ::  and automatically adding adjectives)
  registrations XBOOLE_0, SUBSET_1, ORDINAL1, PARTFUN1,
    RELAT_2, RELAT_1,
    FUNCT_1, FUNCT_2,
    FUNCOP_1, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1, FINSEQ_2, CARD_1, RELSET_1,
    PBOOLE, CARD_3,
    ORDERS_2,
    TREES_4, TREES_3, TREES_2, TREES_1,
    FINSET_1;

  :: Load theorem en definition labels
  theorems FUNCOP_1, RELAT_1, TARSKI, FINSEQ_1, FINSEQ_2, XTUPLE_0, FUNCT_1,
    TREES_4, TREES_3, TREES_2, TREES_1,
    XBOOLE_0, SUBSET_1, RELAT_2, ENUMSET1,
    CARD_1, CARD_2, CARD_3, CARD_4;
  
  schemes RECDEF_1;

  requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;

begin

::: ## Syntax and behaviour

reserve T,T1,T2 for Tree,
        D for DecoratedTree,
        p,q,q2,r for FinSequence of NAT,
        x,y,X,Y for set,
        n for Element of NAT;

::: ### Lambda terms

::: A _lambda term_ is identified with it's AST (abstract
:::  syntax tree). More precisely, we identify a lambda term
:::  with a finite decorated tree.
::: The nodes of the tree denote either application, abstraction
:::  or a variable. Using _De Bruijn_ variable indexing, we decorate
:::  variables with their De Bruijn index (a natural nuber). Note
:::  that we needn't explicitly annotate the type of a node,
:::  as the three types of nodes differ in how many children they must
:::  have: an application always has two children, an abstraction but one,
:::  and variables don't have any. So it suffices to decorate application
:::  and abstraction nodes with some default value (say, $0$).

::: In Mizar, a `Tree` is a non-empty subset of `NAT*`, so a set of finite
:::  sequences of natural numbers, such that certain logical properties hold.
::: Every such sequence denotes a node in the tree, and can be interpreted
:::  as a _path specification_ (how to get there, from the top). We see, then,
:::  that lambda term trees are a specific subset of general trees, in which
:::  only finite sequences of $\\{0,1\\}$ are used to denote nodes, nodes
:::  with children are decorated with a $0$, and is also finite in height.
::: By _decorating_ a node `r` with a number `n`, we mean that `M.r = n`.
::: Here `M` is the `DecoratedTree of NAT` which represents the lambda term.

definition
  let D;
  
  attr D is LambdaTerm-like means
    :Def1:
    dom D is finite &
    for r st r in dom D holds
      r is FinSequence of {0,1} &
      (r^<*0*> in dom D implies D.r = 0);
end;

::: Some simple `LambdaTerm-like` things.

::: TODO: some more of these simple trees, for easy usage
:::  later on, when defining application and abstraction.

registration
  cluster { {} } --> 0 -> LambdaTerm-like;
  coherence
  proof
    set D = { {} } --> 0;
    dom D = { {} } by FUNCOP_1:13;
    hence dom D is finite;
    let r be FinSequence of NAT;
    assume
  A1: r in dom D;
    dom D = { {} } by FUNCOP_1:13;
    then r = {} by A1, TARSKI:def 1;
    then r = <*> {0,1} by FINSEQ_1:def 6;
    hence r is FinSequence of {0,1};
    assume r^<*0*> in dom D;
    thus D.r = 0 by A1, FUNCOP_1:7;
  end;
end;

registration
  cluster { <*> NAT } -> Tree-like;
  coherence
  proof
    { <*> NAT } = { {} } by FINSEQ_1:def 6;
    thus { <*> NAT } is Tree-like;
  end;
end;

registration
  cluster { <*> NAT } --> 0 -> LambdaTerm-like;
  coherence
  proof
    { <*> NAT } = { {} } by FINSEQ_1:def 6;
    thus { <*> NAT } --> 0 is LambdaTerm-like;
  end;
end;

registration
  cluster { {}, <*0*> } -> Tree-like;
  coherence
  proof
    set T = { {}, <*0*> };
    T = elementary_tree 1 by TREES_1:51;
    then reconsider T as Tree;
    T is Tree-like;
    hence { {}, <*0*> } is Tree-like;
  end;
end;

registration
  cluster { {}, <*0*> } --> 0 -> LambdaTerm-like;
  coherence
  proof
    set T = { {}, <*0*> };
  A6: T is finite;
    set D = T --> 0;
  A2: dom D = T by FUNCOP_1:13;
    hence dom D is finite by A6;
    reconsider T as Tree;
    let r be FinSequence of NAT;
    assume
  A1: r in dom D;
    then
  A3: r = {} or r = <*0*> by A1, A2, TARSKI:def 2;
    per cases;
    suppose r = {};
::>       *60
      then r = <*> {0,1} by FINSEQ_1:def 6;
      hence r is FinSequence of {0,1};
      assume r^<*0*> in dom D;
      ::thus D.r = 0 by A1, FUNCOP_1:7;
    end;
    suppose
    A5: not r = {};
      set ZeroOne = {0,1};
      reconsider ZeroOne as non empty set;
      set Zero = 0;
      Zero in {0,1} by TARSKI:def 2;
      then reconsider Zero as Element of ZeroOne;
    A4: <*Zero*> is FinSequence of ZeroOne;
      r = <*0*> by A3, A5
       .= <*Zero*>;
      hence r is FinSequence of {0,1} by A4;
      assume r^<*0*> in dom D;
      thus D.r = 0 by A1, FUNCOP_1:7;
    end;
  end;
end;

registration
  cluster { {}, <*0*>, <*1*> } -> Tree-like;
  coherence
  proof
    set T = { {}, <*0*>, <*1*> };
    T = elementary_tree 2 by TREES_1:53;
    then reconsider T as Tree;
    T is Tree-like;
    hence { {}, <*0*>, <*1*> } is Tree-like;
  end;
end;

registration
  cluster { {}, <*0*>, <*1*> } --> 0 -> LambdaTerm-like;
  coherence
  proof
    set T = { {}, <*0*>, <*1*> };
  A6: T is finite;
    set D = T --> 0;
  A2: dom D = T by FUNCOP_1:13;
    hence dom D is finite by A6;
    reconsider T as Tree;
    let r be FinSequence of NAT;
    assume
  A1: r in dom D;
    then
  A3: r = {} or r = <*0*> or r = <*1*> by A1, A2, ENUMSET1:def 1;
    per cases;
    suppose r = {};
      then r = <*> {0,1} by FINSEQ_1:def 6;
      hence r is FinSequence of {0,1};
      assume r^<*0*> in dom D;
      thus D.r = 0 by A1, FUNCOP_1:7;
    end;
    suppose
    A5: r = <*0*>;
      set ZeroOne = {0,1};
      reconsider ZeroOne as non empty set;
      set Zero = 0;
      Zero in {0,1} by TARSKI:def 2;
      then reconsider Zero as Element of ZeroOne;
    A4: <*Zero*> is FinSequence of ZeroOne;
      r = <*0*> by A5
       .= <*Zero*>;
      hence r is FinSequence of {0,1} by A4;
      assume r^<*0*> in dom D;
      thus D.r = 0 by A1, FUNCOP_1:7;
    end;
    suppose not (r = {} or r = <*0*>);
      then
    A7: r = <*1*> by A3;
      set ZeroOne = {0,1};
      reconsider ZeroOne as non empty set;
      set One = 1;
      One in {0,1} by TARSKI:def 2;
      then reconsider One as Element of ZeroOne;
    A4: <*One*> is FinSequence of ZeroOne;
      r = <*1*> by A7
       .= <*One*>;
      hence r is FinSequence of {0,1};
      assume r^<*0*> in dom D;
      thus D.r = 0 by A1, FUNCOP_1:7;
    end;
  end;
end;

::: And then we can easily define a `LambdaTerm-like` cluster,
:::  and make a mode `LambdaTerm` out of it.
registration
  cluster LambdaTerm-like for DecoratedTree of NAT;
  existence
  proof
    set D = { {} } --> 0;
    take D;
    thus D is LambdaTerm-like;
  end;
end;

definition
  mode LambdaTerm is LambdaTerm-like DecoratedTree of NAT;
end;

definition
  func PreAbstractionTerm -> LambdaTerm equals
    { {}, <*0*> } --> 0;
  coherence
  proof
    set T = { {}, <*0*> };
    reconsider T as Tree;
    set D = T --> 0;
    reconsider D as DecoratedTree of NAT;
    D is LambdaTerm-like;
    then reconsider D as LambdaTerm;
    D is LambdaTerm;
    hence { {}, <*0*> } --> 0 is LambdaTerm;
  end;
end;

definition
  func PreApplicationTerm -> LambdaTerm equals
    { {}, <*0*>, <*1*> } --> 0;
  coherence
  proof
    set T = { {}, <*0*>, <*1*> };
    reconsider T as Tree;
    set D = T --> 0;
    reconsider D as DecoratedTree of NAT;
    D is LambdaTerm-like;
    then reconsider D as LambdaTerm;
    D is LambdaTerm;
    hence { {}, <*0*>, <*1*> } --> 0 is LambdaTerm;
  end;
end;

reserve M,N,P,Q for LambdaTerm;

::: #### Preservation under operations

::: `LambdaTerm`s are preserved under taking subterms, and composing
:::  terms. We'll register this knowledge in the type system.

::: Let Mizar know that `M|r` is also a `LambdaTerm`.
::: The definition of `|` that we are using is the definition
:::  that operates on (and returns) a `DecoratedTree`. Showing
:::  that retricting a `LambdaTerm` produces a `LambdaTerm-like`
:::  result ensures us that the result is a `LambdaTerm`.
registration
  let M be LambdaTerm,
      r be Element of dom M;

  cluster M|r -> LambdaTerm-like;
  coherence
  proof
    thus dom (M|r) is finite proof
      dom M is finite by Def1;
      then (dom M)|r is finite;
      hence thesis by TREES_2:def 10;
    end;

    let p be FinSequence of NAT;
    assume
  A3: p in dom (M|r);
    thus p is FinSequence of {0,1} proof
      dom (M|r) = (dom M)|r by TREES_2:def 10;
      then p in (dom M)|r by A3;
      then r^p in dom M by TREES_1:def 6;
      then r^p is FinSequence of {0,1} by Def1;
      hence thesis by FINSEQ_1:36;
    end;

    :: r^p^<*0*>
    set p0 = p^<*0*>;
    set rp = r^p;
    assume
  A4: p0 in dom (M|r);
    thus (M|r).p = 0 proof
      dom (M|r) is Tree-like;
      then
    A5: p0 in dom (M|r) by A4, TREES_1:21;
      then reconsider p0 as Element of dom (M|r);
      reconsider p0 as Element of (dom M)|r by TREES_2:def 10;
      p0 in (dom M)|r;
      then r^p0 in dom M by TREES_1:def 6;
      then
    A6: rp^<*0*> in dom M by FINSEQ_1:32;
      then rp in dom M by TREES_1:21;
      then M.rp = 0 by A6, Def1;
      then
    A8: M.(r^p) = 0;
      p in (dom M)|r by A3, TREES_2:def 10;
      then (M|r).p = M.(r^p) by TREES_2:def 10;
      hence (M|r).p = 0 by A8, TREES_2:def 10;
    end;
  end;
end;

::: Let Mizar know that `P with-replacement (p,Q)` is also a `LambdaTerm`.
::: First we prove a slightly more general theorem about the ranges of
:::  composed `DecoratedTree`s, and add this knowledge to the type system.
reserve X for set,
        D1,D2 for DecoratedTree,
        p for Element of dom D1;

theorem Th1:
  D1 is X-valued & D2 is Y-valued implies
    D1 with-replacement (p,D2) is (X \/ Y)-valued
proof
  assume
A4: D1 is X-valued;
  assume
A5: D2 is Y-valued;
  set D = D1 with-replacement (p,D2);
  set T = dom D;
  rng D c= X \/ Y
  proof
    let x be set;
    assume x in rng D;
    then ex q being set st [q,x] in D by XTUPLE_0:def 13;
    then consider q being set such that
  A1: [q,x] in D;
  A2: q in T by A1, XTUPLE_0:def 12;
    T is Tree by TREES_2:def 8;
    then reconsider q as FinSequence of NAT by A2, TREES_1:19;
  A13: T = dom D1 with-replacement (p, dom D2) by TREES_2:def 11;
    then
  A3: ((not p is_a_prefix_of q & D.q = D1.q) or
      (ex r st r in dom D2 & q = p^r & D.q = D2.r)) by A2, TREES_2:def 11;
    per cases;
    suppose
    A6: (not p is_a_prefix_of q & D.q = D1.q);
      [q,x] in D by A1;
      then D.q = x by A2, FUNCT_1:def 2;
      then
    A7: D1.q = x by A6;
    A12: q in dom D1
      proof
      A14: (q in dom D1 & not p is_a_proper_prefix_of q or
          ex r st r in dom D2 & q = p^r) by A2, A13, TREES_1:def 9;
        per cases;
        suppose q in dom D1 & not p is_a_proper_prefix_of q;
          hence q in dom D1;
        end;
        suppose not (q in dom D1 & not p is_a_proper_prefix_of q);
          then ex r st r in dom D2 & q = p^r by A14;
          then
        A15: p is_a_prefix_of q by TREES_1:1;
          assume not q in dom D1;
          thus contradiction by A6, A15;
        end;
      end;
      [q,x] in D1 by A7, A12, FUNCT_1:def 2;
      then
    A8: x in rng D1 by XTUPLE_0:def 13;
      rng D1 c= X by A4, RELAT_1:def 19;
      then x in X by A8;
      hence x in (X \/ Y) by XBOOLE_0:def 3;
    end;
    suppose not (not p is_a_prefix_of q & D.q = D1.q);
      then (ex r st r in dom D2 & q = p^r & D.q = D2.r) by A3;
      then consider r such that
    A9: r in dom D2 & q = p^r & D.q = D2.r;
      [q,x] in D by A1;
      then D.q = x by A2, FUNCT_1:def 2;
      then D2.r = x by A9;
      then [r,x] in D2 by A9, FUNCT_1:def 2;
      then
    A11: x in rng D2 by XTUPLE_0:def 13;
      rng D2 c= Y by A5, RELAT_1:def 19;
      then x in Y by A11;
      hence x in (X \/ Y) by XBOOLE_0:def 3;
    end;
  end;
  hence thesis by RELAT_1:def 19;
end;

registration
  let P,Q be LambdaTerm,
      p be Element of dom P;

  cluster P with-replacement (p,Q) -> NAT-valued;
  coherence
  proof
  A1: P is NAT-valued by Def1;
  A2: Q is NAT-valued by Def1;
    P with-replacement (p,Q) is (NAT \/ NAT)-valued by A1, A2, Th1;
    hence P with-replacement (p,Q) is NAT-valued by XBOOLE_0:def 3;
  end;
end;

reserve R for DecoratedTree,
        p for Element of dom P;

theorem Th2:
  P with-replacement (p,Q) is LambdaTerm-like
proof
  set R = P with-replacement (p,Q);
  thus dom R is finite
  proof
  A1: dom P is finite by Def1;
  A2: dom Q is finite by Def1;
    dom R = (dom P) with-replacement (p,dom Q) by TREES_2:def 11;
    hence thesis by A1, A2;
  end;

  let r;
  assume
A3: r in dom R;
  thus r is FinSequence of {0,1}
  proof
    dom R = (dom P) with-replacement (p,dom Q) by TREES_2:def 11;
    then
  A4: ((r in dom P & not p is_a_proper_prefix_of r) or
      (ex q st q in dom Q & r = p^q)) by A3, TREES_1:def 9;
    per cases;
    suppose (r in dom P & not p is_a_proper_prefix_of r);
      hence r is FinSequence of {0,1} by Def1;
    end;
    suppose not (r in dom P & not p is_a_proper_prefix_of r);
      then (ex q st q in dom Q & r = p^q) by A4;
      then consider q such that
    A5: q in dom Q & r = p^q;
      q is FinSequence of {0,1} by A5, Def1;
      then reconsider q as FinSequence of {0,1};
      p is FinSequence of {0,1} by Def1;
      then reconsider p as FinSequence of {0,1};
      p^q is FinSequence of {0,1};
      hence thesis by A5;
    end;
  end;
  set r0 = r^<*0*>;
  assume
A6: r0 in dom R;
  then r0 in (dom P) with-replacement (p,dom Q) by TREES_2:def 11;
  then
A7: ((r0 in (dom P) & not p is_a_proper_prefix_of r0) or
     (ex q st q in (dom Q) & r0 = p^q)) by TREES_1:def 9;
  per cases;
  suppose
  A13: (r0 in (dom P) & not p is_a_proper_prefix_of r0);
    then
  A8: r0 in dom P;
    then
  A12: r in dom P by TREES_1:21;
    then
  A9: P.r = 0 by A8, Def1;
  A10: not p is_a_prefix_of r
    proof
      assume p is_a_prefix_of r;
      then p is_a_proper_prefix_of r0 by TREES_1:8;
      hence contradiction by A13;
    end;
    R.r = P.r by A10, A12, TREES_3:45;
    hence R.r = 0 by A9;
  end;
  suppose not (r0 in (dom P) & not p is_a_proper_prefix_of r0);
    then (ex q st q in (dom Q) & r0 = p^q) by A7;
    then consider q such that
  A14: q in (dom Q) & r0 = p^q;
    per cases;
    suppose ex q2 being FinSequence, n being set st q = q2^<*n*>;
      then consider q2 being FinSequence, n being set such that
    A15: q = q2^<*n*>;
      q2 is FinSequence of NAT by A15, FINSEQ_1:36;
      then reconsider q2 as FinSequence of NAT;
      r^<*0*> = r0
        .= p^q by A14
        .= p^(q2^<*n*>) by A15
        .= (p^q2)^<*n*> by FINSEQ_1:32;
      then
    A19: r = (p^q2) & <*0*> = <*n*> by FINSEQ_2:17;
      then
    A16: 0 = n by FINSEQ_1:76;
      q in dom Q by A14;
      then
    A17: q2^<*0*> in dom Q by A14, A15, A16;
      then
    A18: q2 in dom Q by TREES_1:21;
      then Q.q2 = 0 by Def1, A17;
      then R.(p^q2) = 0 by A18, TREES_3:46;
      hence R.r = 0 by A19;
    end;
    suppose
    A20: not ex q2 being FinSequence, n being set st q = q2^<*n*>;
    A21: q = {}
      proof
        assume q <> {};
        then ex q2 being FinSequence,
          n being set st q = q2^<*n*> by FINSEQ_1:46;
        hence contradiction by A20;
      end;
    A30: r0 = p by A14, A21, FINSEQ_1:34;
      then
    A22: r0 in dom P;
      then
    A23: r in dom P by A22, TREES_1:21;
      then
    A24: P.r = 0 by A22, Def1;
    A27: not p is_a_prefix_of r
      proof
        assume p is_a_prefix_of r;
        then
      A28: p c= r;
        r0 = p by A30;
        then r0 c= p by XBOOLE_0:def 10;
        then r0 is_a_prefix_of p;
        then r is_a_proper_prefix_of p by TREES_1:7;
        then
      A32: r c< p;
        then r c= p by XBOOLE_0:def 8;
        then
      A31: r = p by A28, XBOOLE_0:def 10;
        r <> p by A32, XBOOLE_0:def 8;
        hence contradiction by A31;
      end;
      R.r = P.r by A23, A27, TREES_3:45;
      hence R.r = 0 by A24;
    end;
  end;
end;

registration
  let P,Q be LambdaTerm,
      p be Element of dom P;

  cluster P with-replacement (p,Q) -> LambdaTerm-like;
  coherence
  proof
    thus thesis by Th2;
  end;
end;

definition
  let P be LambdaTerm,
      p be Element of dom P,
      Q be LambdaTerm;

  redefine func P with-replacement (p,Q) -> LambdaTerm;
  coherence;
end;

::: ### Helper definitions
::: Some helper definitions for working with lambda terms.

::: #### Variable occurences
definition
  let M be LambdaTerm,
      r be Node of M;
  
  pred r is_variable_in M means
    not r^<*0*> in dom M;

  pred r is_abstraction_in M means
    r^<*0*> in dom M & not r^<*1*> in dom M;

  pred r is_application_in M means
    r^<*0*> in dom M & r^<*1*> in dom M;
end;

definition
  let M be LambdaTerm,
      r be Node of M;

  func abstr-depth (r,M) -> Element of NAT equals
    card { p where p is Node of M
           : p is_a_proper_prefix_of r &
             p is_abstraction_in M };
  coherence
  proof
    set abstractions = { p where p is Node of M
                         : p is_a_proper_prefix_of r &
                           p is_abstraction_in M };
  end;
::> *70
end;

::: By $r$ being an _active variable_ of some term $M$ (or more precisely,
:::  a path identifying an active variable), I mean that if $\lambda M$ were
:::  to be applied to some other term $N$, we would substitute this variable
:::  by $M$.
definition
  let M be LambdaTerm,
      r be Node of M;
  
  pred r is_active_variable_in M means
    r is_variable_in M &
    M.r = abstr-depth (r,M);
end;

::: #### Abstraction and application

::: Then we can define abstraction and application
definition
  let M;
  
  func Abs(M) -> LambdaTerm means
    it|<*0*> = M & not <*1*> in dom it;
  existence
  proof
    set e = <*0*>;
    dom PreAbstractionTerm = { {}, <*0*> } by FUNCOP_1:13;
    then <*0*> in dom PreAbstractionTerm by TARSKI:def 2;
    then reconsider e as Element of dom PreAbstractionTerm;
    set D = PreAbstractionTerm with-replacement (e, M);

    take D;
    thus D|<*0*> = M;
::>                *4
    thus not <*1*> in dom D;
::>                       *4
  end;
  uniqueness;
::>        *4
end;

definition
  let M,N;

  func App(M,N) -> LambdaTerm means
    it|<*0*> = M & it|<*1*> = N;
  existence
  proof
    set e0 = <*0*>;
    dom PreApplicationTerm = { {}, <*0*>, <*1*> } by FUNCOP_1:13;
    then <*0*> in dom PreApplicationTerm by ENUMSET1:def 1;
    then reconsider e0 as Element of dom PreApplicationTerm;
    set preD = PreApplicationTerm with-replacement (e0, M);
    preD is LambdaTerm;

    set e1 = <*1*>;
  A1: dom preD = (dom PreApplicationTerm) with-replacement (e0, dom M)
      by TREES_2:def 11;
    dom PreApplicationTerm = { {}, <*0*>, <*1*> } by FUNCOP_1:13;
    then
  A2: <*1*> in dom PreApplicationTerm by ENUMSET1:def 1;
    not e0 is_a_proper_prefix_of e1;
::>                               *4
    then <*1*> in dom preD by A1, A2, TREES_1:def 9;
    then reconsider e1 as Element of dom preD;
    set D = preD with-replacement (e1, N);
    D is LambdaTerm;
    
    take D;
    thus D|<*0*> = M;
::>                *4
    thus D|<*1*> = N;
::>                *4
  end;
  uniqueness;
::>        *4
end;

definition
  let M,N;

  func SubstituteActiveVar(M,N) -> LambdaTerm means
    :: most things are the same,
    (for r st not (ex p being Element of dom N
                      st p is_active_variable_in N & p is_a_prefix_of r)
           holds [r,x] in it iff [r,x] in N
    ) &
    :: except that the active variables are replaced with M
    (for r being Element of dom N
            st r is_active_variable_in N
            holds it|r = M);
  existence;
::>       *4
  uniqueness;
::>        *4
end;

::: ### Reduction and conversion

::: First I define _shallow_ one-step beta substitution, which only
:::  states (the De Bruijn equivalent of)
:::  $(\\lambda x.N)M \\to_{\\beta} N[x:=M]$.

definition
  let P,Q;
  
  :: P === (\x.N)M --> N[x:=M] === Q
  pred P beta_shallow Q means
    ex M,N st
      :: P === (\x.N)M
      P = App(Abs(N),M) &

      :: Q === N[x:=M]
      Q = SubstituteActiveVar(N,M);
end;
::: Then we extend this ordinary one-step beta reduction, that is,
:::  any subterm is also allowed to reduce.

::: Note that I had to use a work-around here, where I have two
:::  set-theoretically identical elements `p` and `q`, but considered
:::  in Mizar as having different types to enable the two `|` operations
:::  to work. I also tried defining something like `Element of (dom M),(dom N)`,
:::  but this is extremely difficult, if not impossible.
definition
  let M,N;

  pred M beta N means
    ex
      p being Element of dom M,
      q being Element of dom N st
        p = q &
        M|p beta_shallow N|q &
        for q st not p is_a_prefix_of q holds
          [r,x] in M iff [r,x] in N;
end;

::: We close the relation under transitivity and reflexivity
:::  to get the full beta reduction relation.
definition
  let M,N;
  
  pred M beta* N means
    M = N or
    ex P st M beta P & P beta N;
end;

::: And then close under symmetry to get beta conversion.
definition
  let M,N;
  
  pred M beta= N means
    M beta* N or N beta* M;
end;

::: ## CPO's

reserve x,y,z,z2 for set;

::: We start off by defining
:::  a mode for partial orders upon some set `D`. I don't really
:::  get why de Mizar guys wrote their order definitions the way
:::  they did (in `ORDERS_1`, `ORDERS_2`, etc...)

definition
  let D be set;
  mode PartialOrder of D is transitive reflexive antisymmetric (Relation of D);
end;

::: ### (Least) upper bounds

::: An element $u$ of $D$ is an _upper bound_ of a
:::  subset $X\\subseteq D$ iff for all $x\\in X$,
:::  $x\\sqsubseteq u$.

::: I often encountered \*100 errors (also elsewhere, but most notably)
:::  here. Note: a \*100 error occurs when not all loci (which are variables
:::  introduced by the `let` statement) are used _in the attribute name_.
::: "Transitive" usage _is_ allowed: in the below definition of
:::  upper bounds, the variable `D` is not explicitly stated in the name
:::  of the attribute, however it is used in the `let` introduction rule of
:::  `R`, `X` and `u`, so it is allowed.
::: However, I had to explicitly add `R` to the attribute name, because even
:::  though it is used in the defining formula, it is not included in the
:::  introductory rules of `D`, `X` or `u`.
definition
  let D be set,
      R be PartialOrder of D,
      X be Subset of D,
      u be Element of D;

  attr u is X,R-ub means
    for x st x in X holds [x,u] in R;
end;

::: Least upper bounds
definition
  let D be set,
      R be PartialOrder of D,
      X be Subset of D,
      u be Element of D;

  attr u is X,R-lub means
    u is X,R-ub &
    for u2 being Element of D st u2 is X,R-ub holds
      [u,u2] in R;
end;

::: ### Directed sets

::: If $D$ is a set, and $R$ a partial order on $D$,
:::  then an $X\\subseteq D$ is called _directed_ iff
:::  for all $x,y \\in X$, there is some $z \\in X$,
:::  such that $x\\sqsubseteq z$ and $y\\sqsubseteq z$.
definition
  let D be set,
      R be PartialOrder of D,
      X be Subset of D;

  attr X is R-directed means
    for x,y st x in X & y in X holds
      ex z st
        z in X & [x,z] in R & [y,z] in R;
end;

::: this registration is actually useless..
registration
  let D be set,
      R be PartialOrder of D;

  cluster R-directed for Subset of D;
  existence
  proof
    set X = {};
    reconsider X as Subset of D by SUBSET_1:1;
    take X;
    thus X is R-directed
    proof
      assume not (for x,y st x in X & y in X holds
      ex z st
        z in X & [x,z] in R & [y,z] in R);
      then consider x,y such that
    A1: x in X & y in X and
    A2: not (ex z st z in X & [x,z] in R & [y,z] in R);
      x in X by A1;
      then ex x st x in X;
      hence contradiction by XBOOLE_0:def 1;
    end;
  end;
end;

registration
  let D be non empty set,
      R be PartialOrder of D;

  cluster R-directed for non empty Subset of D;
  existence
  proof
    ex d being set st d in D by XBOOLE_0:def 1;
    then consider d being set such that
  A1: d in D;
    set X = {d};
    d is Element of D by A1;
    then X is Subset of D by SUBSET_1:33;
    then reconsider X as non empty Subset of D;
    take X;
    thus X is R-directed
    proof
      assume not (for x,y st x in X & y in X holds
      ex z st
        z in X & [x,z] in R & [y,z] in R);
      then consider x,y such that
    A2: x in X & y in X and
    A3: not (ex z st z in X & [x,z] in R & [y,z] in R);
      per cases;
      suppose x = y;
        :: idea: use x = y = d to show that
        ::  ex d, because [x,d] = [x,x] in R by reflexivity, ..
        thus contradiction;
::>                      *1
      end;
      suppose x <> y;
        per cases;
        suppose x = d;
          :: idea: then d <> y, but both in X,
          ::  but that contradicts X being a singleton
          thus contradiction;
::>                        *1
        end;
        suppose x <> d;
          :: idea: then d <> x, but both in X,
          ::  but that contradicts X being a singleton
          thus contradiction;
::>                        *1
        end;
      end;
    end;
  end;
end;

::: ### Completeness

::: A partial ordered set is complete iff every directed set has
:::  a least upper bound.
definition
  let D be non empty set,
      R be PartialOrder of D;

  attr R is complete means
    for X being R-directed non empty Subset of D holds
      ex u being Element of D st u is X,R-lub;
end;

::: Here again, what I want to express is that such complete partial
:::  orders exist, not that for every $D$, any partial order is $D$-complete,
:::  or something weird like that...
:::
::: What are the precise semantics of these clusters?
registration
  let D be non empty set;

  cluster complete for PartialOrder of D;
  existence
  proof
    set I = id D;
    reconsider I as PartialOrder of D;
    take I;
    thus I is complete
    proof
      let X be I-directed Subset of D;
::>                            *56
    end;
  end;
end;

definition
  let D be non empty set,
      R be complete PartialOrder of D,
      X be R-directed Subset of D;

  func LUB(X) -> Element of D means
    it is X,R-lub;
  existence;
::>       *4
  uniqueness;
::>        *4
end;

::: ### Bottom element
definition
  let D be set,
      R be PartialOrder of D,
      b be Element of D;

  attr b is R-bottom means
    for y st y in D holds [b,y] in R;
end;

registration
  let D be set,
      R be PartialOrder of D;

  cluster R-bottom for Element of D;
  existence;
::>       *4
end;

::: ### The CPO structure
definition
  struct CPO (#
    carrier -> non empty set,
    BinRel -> complete PartialOrder of (the carrier),
    Bot -> (the BinRel)-bottom Element of the carrier
  #);
end;

::: ### Continuity
::: #### Monotonicity
definition
  let D,E be CPO,
      f be Function of (the carrier of D),(the carrier of E);

  attr f is monotone means
    for x,y st [x,y] in (the BinRel of D) holds
      [f.x,f.y] in (the BinRel of E);
end;

::: #### Continuity
definition
  let D,E be CPO,
      f be Function of (the carrier of D),(the carrier of E);

  attr f is continuous means
    f is monotone &
    for X being (the BinRel of D)-directed
                Subset of (the carrier of D)
      holds
        f.LUB(X) = LUB(f.:X);
::>                  *103
::                 This is indeed somewhat complicated,
::                  why would it necessarily be directed,
::                  and thus eligible for LUB ?
end;

registration
  let D,E be CPO;

  cluster continuous for Function of (the carrier of D),(the carrier of E);
  existence;
::>       *4
end;

definition
  let D,E be CPO;

  mode ContinuousFunc of D,E is continuous Function of
    (the carrier of D),(the carrier of E);
end;

::: #### Continuous function space
definition
  let D,E be CPO;

  func ContinuousFuncs(D,E) -> set means
    x in it iff x is ContinuousFunc of D,E;
  existence;
::>       *4
  uniqueness;
::>        *4
end;

definition
  let D,E be CPO,
      f,g be ContinuousFunc of D,E;

  pred f piecewise_le g means
    for x being Element of D holds
      [f.x, g.x] in (the BinRel of E);
end;

theorem
  for D,E being CPO holds
    CPO(# ContinuousFuncs(D,E),
          { [f,g] where f,g is ContinuousFunc of D,E : f piecewise_le g },
          {} #) is CPO
::>           *105
proof
  thus thesis;
end;

::: #### The powerset CPO
definition
  let A be non empty set;

  func SubsetRelation(A) -> Relation of bool A means
    [x,y] in it iff x is Subset of y;
  existence;
::>       *4
  uniqueness;
::>        *4
end;

theorem
  for A being non empty set holds
    CPO(# bool A, SubsetRelation(A), {} #) is CPO
::>                                      *105
proof
  thus thesis;
end;



::: ## Graph Models

definition
  let Bn be non empty set;
  func BnNext(Bn) -> set equals Bn \/ [: Bn*, Bn :];
  coherence;
end;

definition
  let A be non empty set;
  func Web(A) -> set equals A;
  :: TODO inductive construction
  coherence;
end;

theorem
  for A being non empty set holds
    id Web(A) is one-to-one
proof
  thus thesis;
end;

::: Doesn't there already exist a notation for the set of all
:::  finite subsets of a set in Mizar ?!
definition
  let B be set;

  func (B)^<w -> set equals
    { X where X is Subset of B : X is finite };
  coherence;
end;

definition
  let B be set;

  redefine func (B)^<w -> set means
    X in it iff X c= B & X is finite;
  compatibility
  proof
    thus thesis;
::>           *4,4,4
  end;
  coherence;
end;

::: This is true if one assumes the axiom of choice, and
:::  then still it might be a bit complicated to give a proof.
::: (First show that `B` can be well-ordered, then define a
:::  well-order on `[:B^<w,B:]`, so that these two well-orders
:::  become isomorphic..?)
registration
  let B be infinite set;
  cluster one-to-one for Function of [:B^<w,B:],B;
  existence
  proof
    not B is finite;
    then [:B,B:],B are_equipotent by CARD_4:18; :: <-- though this
        :: won't get me there directly :(
    :: I'll probably "build up" (first finite sets of size 1, then 2, ...)
    thus ex f being Function of [:B^<w,B:],B st f is one-to-one;
::>                                                           *4
  end;
end;

::: Easier, and probably good enough for our purposes, is to prove
:::  this version, where we only prove it for countable sets.
registration
  let B be countable infinite set;
  cluster one-to-one for Function of [:B^<w,B:],B;
  existence
  proof
    thus ex f being Function of [:B^<w,B:],B st f is one-to-one;
::>                                                           *4
  end;
end;

:: Graph models
definition
  struct GraphModel (#
    carrier -> infinite set,
    c -> one-to-one (Function of
      [:(the carrier)^<w, the carrier:], the carrier)
  #);
end;


::>
::> 1: It is not true
::> 4: This inference is not accepted
::> 56: Disagreement of types
::> 60: Something remains to be proved in this case
::> 70: Something remains to be proved
::> 103: Unknown functor
::> 105: Illegal projection
